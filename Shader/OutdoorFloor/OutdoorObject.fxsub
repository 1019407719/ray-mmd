#ifndef PI_2
#   define PI_2 (3.141592654f * 2.0)
#endif

#ifndef InvPIE
#   define InvPIE 0.318309886142f
#endif

#define MIDPOINT_8_BIT (127.0f / 255.0f)

float4x4 matView : VIEW;
float4x4 matWorldView : WORLDVIEW;
float4x4 matWorldViewProjMatrix : WORLDVIEWPROJECTION;
float4x4 matWorldMirror: CONTROLOBJECT < string Name = "(OffscreenOwner)"; >;

float4 MaterialDiffuse : DIFFUSE  < string Object = "Geometry"; >;
float  MaterialPower   : SPECULARPOWER < string Object = "Geometry"; >;

float4 TextureAddValue : ADDINGTEXTURE;
float4 TextureMulValue : MULTIPLYINGTEXTURE;
float3 CameraPosition : POSITION  < string Object = "Camera"; >;

float EnvRotateX : CONTROLOBJECT < string name="ray_controller.pmx"; string item = "EnvRotateX"; >;
float EnvRotateY : CONTROLOBJECT < string name="ray_controller.pmx"; string item = "EnvRotateY"; >;
float EnvRotateZ : CONTROLOBJECT < string name="ray_controller.pmx"; string item = "EnvRotateZ"; >;

static float3 MirrorPos = float3(0.0, 0.0, 0.0);
static float3 MirrorNormal = float3(0.0, 1.0, 0.0);
static float3 WorldMirrorPos = mul(float4(MirrorPos, 1.0f), matWorldMirror).xyz;
static float3 WorldMirrorNormal = normalize(mul(MirrorNormal, (float3x3)matWorldMirror));

texture DiffuseMap: MATERIALTEXTURE;
sampler DiffuseMapSamp = sampler_state
{
    texture = <DiffuseMap>;
    MINFILTER = LINEAR;
    MAGFILTER = LINEAR;
    MIPFILTER = LINEAR;
    ADDRESSU  = WRAP;
    ADDRESSV  = WRAP;
};

float3 TransMirrorPos(float3 Pos)
{
    Pos -= WorldMirrorNormal * 2.0f * dot(WorldMirrorNormal, Pos - WorldMirrorPos);
    return Pos;
}

float4 TransMirrorPos(float4 Pos)
{
    Pos.xyz -= WorldMirrorNormal * 2.0f * dot(WorldMirrorNormal, Pos.xyz - WorldMirrorPos);
    return Pos;
}

float3 srgb2linear(float3 rgb)
{
    const float ALPHA = 0.055f;
    return rgb < 0.04045f ? rgb / 12.92f : pow((rgb + ALPHA) / (1 + ALPHA), 2.4f);
}

float4 srgb2linear(float4 rgba)
{
    return float4(srgb2linear(rgba.rgb), rgba.a);
}

float IsFace(float4 Pos)
{
    return min(dot(Pos.xyz - WorldMirrorPos, WorldMirrorNormal), dot(CameraPosition-WorldMirrorPos, WorldMirrorNormal));
}

float3 rgb2ycbcr(float3 col)
{
    float3 encode;
    encode.x = dot(float3(0.299, 0.587, 0.114),   col.rgb);
    encode.y = dot(float3(-0.1687, -0.3312, 0.5), col.rgb);
    encode.z = dot(float3(0.5, -0.4186, -0.0813), col.rgb);
    return float3(encode.x, encode.y * MIDPOINT_8_BIT + MIDPOINT_8_BIT, encode.z * MIDPOINT_8_BIT + MIDPOINT_8_BIT);
}

float2 EncodeNormal(float3 n)
{
    float f = sqrt(8*n.z+8);
    return n.xy / f + 0.5;
}

float2 EncodeYcbcr(float4 screenPosition, float3 color)
{
    bool pattern = (fmod(screenPosition.x, 2.0) == fmod(screenPosition.y, 2.0));

    color = rgb2ycbcr(color);

    float2 result = 0.0f;
    result.r = color.r;
    result.g = (pattern) ? color.g: color.b;
    return result;   
}

float3 DecodeRGBM(in float4 rgbm)
{
    return 6 * rgbm.rgb * rgbm.a;
}

float3x3 makeRotate(float eulerX, float eulerY, float eulerZ)
{
    float sj, cj, si, ci, sh, ch;

    sincos(eulerX, si, ci);
    sincos(eulerY, sj, cj);
    sincos(eulerZ, sh, ch);

    float cc = ci * ch;
    float cs = ci * sh;
    float sc = si * ch;
    float ss = si * sh;

    float a1 = cj * ch;
    float a2 = sj * sc - cs;
    float a3 = sj * cc + ss;

    float b1 = cj * sh;
    float b2 = sj * ss + cc;
    float b3 = sj * cs - sc;

    float c1 = -sj;
    float c2 = cj * si;
    float c3 = cj * ci;
    
    float3x3 rotate;
    rotate[0] = float3(a1, a2, a3);
    rotate[1] = float3(b1, b2, b3);
    rotate[2] = float3(c1, c2, c3);
    
    return rotate;
}

float4 GetAlbedo(float2 uv, bool use_texture)
{
#if SKYBOX_ENABLE
    #if SKYBOX_HDR_ENABLE
        return float4(tex2D(DiffuseMapSamp, uv).rgb, 1);
    #else
        return float4(srgb2linear(tex2D(DiffuseMapSamp, uv).rgb), 1);
    #endif
#else
    float4 diffuse = MaterialDiffuse;

    if (use_texture)
    {
        float4 TexColor = tex2D(DiffuseMapSamp, uv);
        TexColor.rgb = lerp(1, TexColor * TextureMulValue + TextureAddValue, TextureMulValue.a + TextureAddValue.a).rgb;
        TexColor.rgb = TexColor.rgb;
        diffuse.rgb *= TexColor.rgb;
    }
    
    return srgb2linear(diffuse);
#endif
}

float2 computeSphereCoord(float3 normal)
{
    float2 coord = float2(1 - (atan2(normal.x, normal.z) * InvPIE * 0.5f + 0.5f), acos(normal.y) * InvPIE);
    return coord;
}

static float rotateX = EnvRotateX * PI_2;
static float rotateY = EnvRotateY * PI_2;
static float rotateZ = EnvRotateZ * PI_2;
static float3x3 rotate = makeRotate(rotateX, rotateY, rotateZ);

void OutdoorObjectVS(
    float4 Position : POSITION,
    float3 Normal : NORMAL,
    float2 Texcoord : TEXCOORD0,
    out float2 oTexcoord : TEXCOORD0,
    out float3 oNormal   : TEXCOORD1,
    out float4 oWorldPos : TEXCOORD2,
    out float4 oPosition : POSITION)
{
#if SKYBOX_ENABLE
    oNormal = normalize(Position);
    oNormal = mul(oNormal, (float3x3)matWorldView);
    oWorldPos = float4(oNormal * length(Position), 1);
#else
    oWorldPos = Position;
    oNormal = mul(Normal, (float3x3)matView);
#endif
    oPosition = mul(TransMirrorPos(oWorldPos), matWorldViewProjMatrix);
    oPosition.x = -oPosition.x;
    oTexcoord = Texcoord;
}

float4 OutdoorObjectPS(
    in float2 coord : TEXCOORD0,
    in float3 normal   : TEXCOORD1,
    in float4 position : TEXCOORD2,
    in float4 screenPosition : SV_Position,
    uniform bool use_texture) : COLOR
{
    normal = normalize(normal);
#if SKYBOX_ENABLE
    normal = normalize(mul(rotate, normal));
    coord = computeSphereCoord(normal);
#endif
    float4 color = GetAlbedo(coord, use_texture);
    
    clip(IsFace(position));
    clip(color.a - 0.01);

    return float4(EncodeNormal(normal), EncodeYcbcr(screenPosition, color));
}

#define OBJECT_TEC(name, mmdpass, tex) \
    technique name<string MMDPass = mmdpass; bool UseTexture = tex; \
    > { \
        pass DrawObject { \
            AlphaTestEnable = false; AlphaBlendEnable = false; \
            VertexShader = compile vs_3_0 OutdoorObjectVS(); \
            PixelShader  = compile ps_3_0 OutdoorObjectPS(tex); \
        } \
    }

OBJECT_TEC(MainTec0, "object", false)
OBJECT_TEC(MainTec1, "object", true)
OBJECT_TEC(MainTec2, "object", false)
OBJECT_TEC(MainTec3, "object", true)
OBJECT_TEC(MainTecBS0, "object_ss", false)
OBJECT_TEC(MainTecBS1, "object_ss", true)
OBJECT_TEC(MainTecBS2, "object_ss", false)
OBJECT_TEC(MainTecBS3, "object_ss", true)

technique EdgeTec < string MMDPass = "edge"; > {}
technique ShadowTec < string MMDPass = "shadow"; > {}
technique ZplotTec < string MMDPass = "zplot"; > {}