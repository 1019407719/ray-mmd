#include "../../ray.conf"
#include "../../shader/math.fxsub"
#include "../../shader/common.fxsub"
#include "../../shader/gbuffer.fxsub"
#include "../../shader/gbuffer_sampler.fxsub"
#include "../../shader/lighting.fxsub"
#include "../../shader/shadowcommon.fxsub"

float mR : CONTROLOBJECT < string name="(self)"; string item = "R+"; >;
float mG : CONTROLOBJECT < string name="(self)"; string item = "G+"; >;
float mB : CONTROLOBJECT < string name="(self)"; string item = "B+"; >;
float mRangeP : CONTROLOBJECT < string name="(self)"; string item = "Range+"; >;
float mAngleM : CONTROLOBJECT < string name="(self)"; string item = "Angle-"; >;
float mIntensityP : CONTROLOBJECT < string name="(self)"; string item = "Intensity+"; >;
float mIntensityM : CONTROLOBJECT < string name="(self)"; string item = "Intensity-"; >;
float mBlink : CONTROLOBJECT < string name="(self)"; string item = "Blink"; >;
float3 mPosition : CONTROLOBJECT < string name="(self)"; string item = "Position"; >;
float3 mDirection : CONTROLOBJECT < string name="(self)"; string item = "Direction"; >;

static float3 LightPosition = mPosition;
static float3 LightDirection = normalize(mDirection - mPosition);
static float LightIntensity = (1 - mIntensityM) * LIGHTSOURCE_MIN_INTENSITY + mIntensityP * LIGHTSOURCE_MAX_INTENSITY;
static float LightRange = 1 + mRangeP * LIGHTSOURCE_MAX_RANGE;
static float LightSpotAngle = radians(lerp(60.0f, 0.0f, mAngleM));
static float3 viewLightPosition = mul(float4(mDirection, 1), matView).xyz;
static float3 viewLightDirection = normalize(mul(LightDirection, (float3x3)matView));

#if SHADOW_MAP_ENABLE
#if SHADOW_MAP_QUALITY == 0
#   define SPOT_SHADOW_MAP_SIZE 256
#elif SHADOW_MAP_QUALITY == 1
#   define SPOT_SHADOW_MAP_SIZE 512
#elif SHADOW_MAP_QUALITY >= 2
#   define SPOT_SHADOW_MAP_SIZE 1024
#elif SHADOW_MAP_QUALITY >= 3
#   define SPOT_SHADOW_MAP_SIZE 2048
#else
#   define SPOT_SHADOW_MAP_SIZE 256
#endif

texture ShadowMap: OFFSCREENRENDERTARGET <
    string Description = "Shadow Rendering";
    int Width = SPOT_SHADOW_MAP_SIZE;
    int Height = SPOT_SHADOW_MAP_SIZE;
    string Format = "R32F";
    float4 ClearColor = { 0, 0, 0, 0 };
    float ClearDepth = 1.0;
    int MipLevels = 1;
    string DefaultEffect = 
        "self = hide;"
        "skybox*.pmx = hide;"
        "ray_controller.pmx=hide;"
        "*.pmx = spot_shadow.fx;"
        "*.pmd = spot_shadow.fx;"
        "* = hide";
>;
sampler ShadowMapSamp = sampler_state {
    texture = <ShadowMap>;
    MinFilter = LINEAR;
    MagFilter = LINEAR;
    MipFilter = NONE;
    AddressU = CLAMP;
    AddressV = CLAMP;
};

static float2 ShadowmapOffset  = float2(0.5 / SPOT_SHADOW_MAP_SIZE, 0.5 / SPOT_SHADOW_MAP_SIZE);
static float2 ShadowmapOffset2 = float2(1.0 / SPOT_SHADOW_MAP_SIZE, 1.0 / SPOT_SHADOW_MAP_SIZE);

float ESM(float3 viewPosition) 
{
    float4 worldPosition = mul(float4(viewPosition, 1), matViewInverse);
#if SPOTLIGHT_ENABLE
    float4 shadowPosition = mul(worldPosition, GetLightViewMatrix(LightDirection, mDirection));
#else
    float4 shadowPosition = mul(worldPosition, GetLightViewMatrix(LightDirection, LightPosition));
#endif
    float4 proj = CalcLightProjPos(LightSpotAngle, LightPlaneNear, LightPlaneFar, shadowPosition);
    
    proj.xy = PosToCoord(proj.xy / proj.w);
    proj.xy += ShadowmapOffset;
    proj.z = exp(-proj.z * 0.5);

    float shadow = saturate(tex2D(ShadowMapSamp, proj.xy).r * proj.z);
    return saturate(shadow);
}

float PCF(float3 viewPosition, int samples) 
{
    float4 worldPosition = mul(float4(viewPosition, 1), matViewInverse);
    float4 shadowPosition = mul(worldPosition, GetLightViewMatrix(LightDirection, mDirection));
    float4 proj = CalcLightProjPos(LightSpotAngle, LightPlaneNear, LightPlaneFar, shadowPosition);
    
    proj.xy = PosToCoord(proj.xy / proj.w);
    proj.xy += ShadowmapOffset;
    proj.z = exp(-proj.z * 0.5);

    float shadow = 0.0;

    [unroll]
    for (int x = -samples; x <= samples; x++) 
    {
        [unroll]
        for (int y = -samples; y <= samples; y++)
        {
            float2 pos = proj.xy + float2(x, y) * ShadowmapOffset2;
            shadow += saturate(tex2D(ShadowMapSamp, pos).r * proj.z);
        }
    }
    
    samples = samples * 2 + 1;
    
    return saturate(shadow / (samples * samples));
}

#endif

float4 ShadingMaterial(MaterialParam material, float3 V)
{
    float3 P = V * material.linearDepth / V.z;
    float3 L = viewLightPosition - P;
    float3 L2 = normalize(L);
    
    float atten1 = GetPhysicalLightAttenuation(L, LightRange);
    atten1 *= GetSpotLightAttenuation(L, viewLightDirection, cos(LightSpotAngle), 2, LightRange);
    
    float atten2 = saturate(dot(material.normal, L2)) * atten1;
    
    float4 lighting = 0.0f;
    lighting.rgb = atten1 * material.albedo * DiffuseBRDF(material.normal, L2, V, material.smoothness, material.transmittance);
    lighting.rgb += atten2 * SpecularBRDF(material.normal, L2, V, material.smoothness, material.specular);
    lighting.rgb *= float3(mR, mG, mB) * LightIntensity * any(material.albedo + material.specular);
    lighting *= saturate(1 - SmoothTriangleWave(time * (mBlink * 10)));
    
    return lighting;
}

float4 ShadingMaterialShadow(MaterialParam material, float3 V)
{
    float4 lighting = ShadingMaterial(material, V);
    
#if SHADOW_MAP_ENABLE
    float3 P = V * material.linearDepth / V.z;
    if (any(lighting))
    {
#if SHADOW_MAP_SOFT_QUALITY == 1
    lighting *= PCF(P, 1);
#elif SHADOW_MAP_SOFT_QUALITY >= 2
    lighting *= PCF(P, 2);
#elif SHADOW_MAP_SOFT_QUALITY >= 3
    lighting *= PCF(P, 3);
#else
    lighting *= ESM(P);
#endif
    }
#endif

    return lighting;
}

void LightSourceVS(
    in float4 Position : POSITION,
    in float3 Normal : NORMAL,
    in float2 Texcoord : TEXCOORD0,
    out float4 oTexcoord0 : TEXCOORD0,
    out float4 oTexcoord1 : TEXCOORD1,
    out float4 oPosition  : POSITION)
{
    if (length(Position.xyz - LightPosition) > 0.1)
    {
        Position.xyz = lerp(mDirection, Position.xyz + normalize(Position.xyz - mDirection) * (1.2 + LightSpotAngle), LightSpotAngle);
    }
    
    Position.xyz = LightPosition + (Position.xyz - LightPosition) * LightRange / 2;
    oTexcoord0 = oPosition = mul(Position, matViewProject);
    oTexcoord1 = -mul(Position, matView);
}

void LightSourcePS(
    float4 texcoord : TEXCOORD0, 
    float3 viewdir : TEXCOORD1,
    out float4 oColor0 : COLOR0,
    out float4 oColor1 : COLOR1)
{
    float2 coord = texcoord.xy / texcoord.w;
    coord = PosToCoord(coord);
    coord += ViewportOffset;

    float4 MRT1 = tex2D(Gbuffer1Map, coord);
    float4 MRT2 = tex2D(Gbuffer2Map, coord);
    float4 MRT3 = tex2D(Gbuffer3Map, coord);
    float4 MRT4 = tex2D(Gbuffer4Map, coord);

    MaterialParam material;
    DecodeGbuffer(MRT1, MRT2, MRT3, MRT4, material);
    
    float4 MRT5 = tex2D(Gbuffer5Map, coord);
    float4 MRT6 = tex2D(Gbuffer6Map, coord);
    float4 MRT7 = tex2D(Gbuffer7Map, coord);
    float4 MRT8 = tex2D(Gbuffer8Map, coord);

    MaterialParam materialAlpha;
    DecodeGbuffer(MRT5, MRT6, MRT7, MRT8, materialAlpha);
    
    float3 V = normalize(viewdir);
    
    oColor0 = ShadingMaterialShadow(material, V);
    oColor1 = ShadingMaterialShadow(materialAlpha, V);
}

void LightSourceShadowlessPS(
    float4 texcoord : TEXCOORD0, 
    float3 viewdir : TEXCOORD1,
    out float4 oColor0 : COLOR0,
    out float4 oColor1 : COLOR1)
{
    float2 coord = texcoord.xy / texcoord.w;
    coord = PosToCoord(coord);
    coord += ViewportOffset;

    float4 MRT1 = tex2D(Gbuffer1Map, coord);
    float4 MRT2 = tex2D(Gbuffer2Map, coord);
    float4 MRT3 = tex2D(Gbuffer3Map, coord);
    float4 MRT4 = tex2D(Gbuffer4Map, coord);

    MaterialParam material;
    DecodeGbuffer(MRT1, MRT2, MRT3, MRT4, material);
    
    float4 MRT5 = tex2D(Gbuffer5Map, coord);
    float4 MRT6 = tex2D(Gbuffer6Map, coord);
    float4 MRT7 = tex2D(Gbuffer7Map, coord);
    float4 MRT8 = tex2D(Gbuffer8Map, coord);

    MaterialParam materialAlpha;
    DecodeGbuffer(MRT5, MRT6, MRT7, MRT8, materialAlpha);
    
    float3 V = normalize(viewdir);
    
    oColor0 = ShadingMaterial(material, V);
    oColor1 = ShadingMaterial(materialAlpha, V);
}

technique MainTech0 < string MMDPass = "object";
    string Script = 
        "RenderColorTarget0=;"
        "RenderColorTarget1=LightAlphaMap;"
        "Pass=DrawObject;"
;>{
    pass DrawObject {
        ZEnable = false; ZWriteEnable = false;
        AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
        SrcBlend = ONE; DestBlend = ONE;
        CullMode = CW;
        VertexShader = compile vs_3_0 LightSourceVS();
        PixelShader  = compile ps_3_0 LightSourceShadowlessPS();
    }
}

technique MainTecBS0 < string MMDPass = "object_ss";
    string Script = 
        "RenderColorTarget0=;"
        "RenderColorTarget1=LightAlphaMap;"
        "Pass=DrawObject;"
;>{
    pass DrawObject {
        ZEnable = false; ZWriteEnable = false;
        AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
        SrcBlend = ONE; DestBlend = ONE;
        CullMode = CW;
        VertexShader = compile vs_3_0 LightSourceVS();
        PixelShader  = compile ps_3_0 LightSourcePS();
    }
}

technique EdgeTec < string MMDPass = "edge"; > {}
technique ShadowTech < string MMDPass = "shadow";  > {}
technique ZplotTec < string MMDPass = "zplot"; > {}