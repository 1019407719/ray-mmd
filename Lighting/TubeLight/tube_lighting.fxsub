#include "../../ray.conf"
#include "../../shader/math.fx"
#include "../../shader/common.fx"
#include "../../shader/gbuffer.fx"
#include "../../shader/gbuffer_sampler.fx"
#include "../../shader/lighting.fx"

#define TUBELIGHT_MAX_WIDTH 55
#define TUBELIGHT_MAX_RADIUS 10

float mR : CONTROLOBJECT < string name="(self)"; string item = "R+"; >;
float mG : CONTROLOBJECT < string name="(self)"; string item = "G+"; >;
float mB : CONTROLOBJECT < string name="(self)"; string item = "B+"; >;
float mRangeP : CONTROLOBJECT < string name="(self)"; string item = "Range+"; >;
float mIntensityP : CONTROLOBJECT < string name="(self)"; string item = "Intensity+"; >;
float mIntensityM : CONTROLOBJECT < string name="(self)"; string item = "Intensity-"; >;
float3 mPosition : CONTROLOBJECT < string name="(self)"; string item = "Position"; >;
float3 mDirection : CONTROLOBJECT < string name="(self)"; string item = "Direction"; >;

static float3 LightPosition = mPosition;
static float3 LightDirection = normalize(mDirection - mPosition);
static float LightIntensity = (1 - mIntensityM) * LIGHTSOURCE_MIN_INTENSITY + mIntensityP * LIGHTSOURCE_MAX_INTENSITY;

float mWidthP : CONTROLOBJECT < string name="(self)"; string item = "Width+"; >;
float mRadiusM : CONTROLOBJECT < string name="(self)"; string item = "Radius-"; >;
float mRadiusP : CONTROLOBJECT < string name="(self)"; string item = "Radius+"; >;

float3 mLeft : CONTROLOBJECT < string name="(self)"; string item = "Left"; >;
float3 mRight : CONTROLOBJECT < string name="(self)"; string item = "Right"; >;

static float LightWidth = (1 + mWidthP * TUBELIGHT_MAX_WIDTH);
static float LightRadius = (1 + mRadiusP * TUBELIGHT_MAX_RADIUS - mRadiusM);
static float LightRange = (1 + mRadiusP * TUBELIGHT_MAX_RADIUS - mRadiusM + mRangeP * LIGHTSOURCE_MAX_RANGE);
static float3 LightLeft = mLeft - (mLeft - mPosition) * LightWidth;
static float3 LightRight = mRight + (mLeft - mPosition) * LightWidth;

void LightSourceVS(
    in float4 Position : POSITION,
    in float3 Normal : NORMAL,
    in float2 Texcoord : TEXCOORD0,
    out float4 oTexcoord0 : TEXCOORD0,
    out float4 oTexcoord1 : TEXCOORD1,
    out float4 oPosition  : POSITION)
{
    Position.xyz = LightPosition + (Position.xyz - LightPosition) * 10000;
    oTexcoord0 = oPosition = mul(Position, matViewProject);
    oTexcoord1 = -mul(Position, matView);
}

float4 LightSourcePS(float4 texcoord : TEXCOORD0, float3 viewdir : TEXCOORD1) : COLOR
{
    float2 coord = texcoord.xy / texcoord.w;
    coord = PosToCoord(coord);
    coord += ViewportOffset;

    float4 MRT0 = tex2D(Gbuffer1Map, coord);
    float4 MRT1 = tex2D(Gbuffer2Map, coord);
    float4 MRT2 = tex2D(Gbuffer3Map, coord);
    float4 MRT3 = tex2D(Gbuffer4Map, coord);

    MaterialParam material;
    DecodeGbuffer(MRT0, MRT1, MRT2, MRT3, material);
    
    float3 V = normalize(viewdir);
    float3 P = V * tex2D(Gbuffer4Map, coord).r / V.z;
    float3 L0 = mul(float4(LightLeft, 1), matView).xyz - P;
    float3 L1 = mul(float4(LightRight, 1), matView).xyz - P;
    float3 L = TubeLightDirection(material.normal, V, L0, L1, P, LightRadius);
    float3 L2 = normalize(L);
    
    float atten = GetPhysicalLightAttenuation(L, LightRange);
    float vis = saturate(dot(material.normal, L2)) * atten;
    
    float4 lighting = 0.0f;    
    lighting.rgb = atten * DiffuseBRDF(material.normal, L2, V, material.smoothness, material.transmittance);    
    lighting.rgb *= material.albedo;
    lighting.rgb += vis * TubeLightBRDF(P, material.normal, V, L0, L1, LightWidth, LightRadius, material.smoothness, material.specular);
    lighting.rgb *= float3(mR, mG, mB) * LightIntensity * any(material.albedo + material.specular);
    return lighting;
}

#define OBJECT_TEC(name, mmdpass) \
    technique name < string MMDPass = mmdpass;\
    > { \
        pass DrawObject { \
            ZEnable = false; ZWriteEnable = false;\
            AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;\
            SrcBlend = ONE; DestBlend = ONE;\
            CullMode = CW;\
            VertexShader = compile vs_3_0 LightSourceVS(); \
            PixelShader  = compile ps_3_0 LightSourcePS(); \
        } \
    }

OBJECT_TEC(MainTec0, "object")
OBJECT_TEC(MainTecBS0, "object_ss")

technique EdgeTec < string MMDPass = "edge"; > {}
technique ShadowTech < string MMDPass = "shadow";  > {}
technique ZplotTec < string MMDPass = "zplot"; > {}